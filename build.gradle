/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories { 
        jcenter()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE'
     }
}

apply plugin: 'jacoco'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.unbroken-dome.test-sets'
    apply plugin: 'io.spring.dependency-management'
    
    repositories { 
        jcenter()
    }
    
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.1.RELEASE'
        }
        dependencies {
            dependencySet (group: 'com.sun.faces', version: '2.2.12') {
                entry 'jsf-api'
                entry 'jsf-impl'
            }
        }
    }
        
    jacoco { 
        toolVersion = '0.7.5.201505241946' 
    }

    testSets { 
        integrationTest { 
            dirName = 'test-integration' 
        }
    }

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter'
        compile('org.springframework.boot:spring-boot-starter-web') {
            exclude module: 'spring-boot-starter-tomcat'
        }
        compile 'org.springframework.boot:spring-boot-starter-undertow'
        compile 'io.undertow:undertow-core:1.3.12.Final'
        compile 'io.undertow:undertow-servlet:1.3.12.Final'
        
        compile 'com.sun.faces:jsf-api'
        compile 'com.sun.faces:jsf-impl'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
  
        integrationTestCompile sourceSets.test.output
    }

    test {
        testLogging { 
            exceptionFormat 'full' 
        }
        jacoco {
            destinationFile = file("$buildDir/jacoco/${name}.exec")
        }
        reports {
            html.enabled = false
            junitXml.enabled = true
        }

        outputs.upToDateWhen { false }
    }

    integrationTest {
        testLogging { 
            exceptionFormat 'full' 
        }
        jacoco {
            destinationFile = file("$buildDir/jacoco/${name}.exec")
        }
        reports {
            html.enabled = false
            junitXml.enabled = true
        }

        outputs.upToDateWhen { false }
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}
